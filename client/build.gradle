buildscript {
	ext.kotlinVersion = '0.12.613'
	repositories {
		jcenter()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
	}
}
apply plugin: 'kotlin2js'

repositories {
	jcenter()
}

evaluationDependsOn(':shared')

dependencies {
	compile "org.jetbrains.kotlin:kotlin-js-library:$kotlinVersion"
}

project(":shared").sourceSets.main.kotlin.srcDirs.each {
	sourceSets.main.kotlin.srcDir it
}

compileKotlin2Js.kotlinOptions.outputFile = "${projectDir}/web/js/app.js"

task copyJsFilesFromDependencies(dependsOn: build) << {
	configurations.compile.each { File file ->
		copy {
			includeEmptyDirs = false

			from zipTree(file.absolutePath)
			into "${projectDir}/web/js/lib"
			include { fileTreeElement ->
				def path = fileTreeElement.path
				path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
			}
		}
	}
}

